
module = module-decl?  imports? bindings?

module-decl = "module" id ";"

imports = import-decl*

import-decl = "import" id ( "as" id )? ";"

bindings = binding ( ";" binding )*  ";"?

binding = id ( "(" param-list ")" )? "=" expr

param-list = id ( "," id )*

expr =
    id
    |
    qualified-id
    |
    literal
    |
    unary-op expr
    |
    expr binary-op expr
    |
    conditional
    |
    lambda
    |
    func-application
    |
    array-func
    |
    array-enum
    |
    array-concat
    |
    array-application
    |
    array-recursion
    |
    array-size
    |
    "(" expr ")"
    |
    let-expr
    |
    where-expr
    |
    id "=" expr

unary-op = "!" | "-"

binary-op =
    "||" | "&&" | "==" | "!=" | "<" | "<=" | ">" | ">=" |
    "+" | "-" | "*" | "/" | "//" | "%" | "^"

conditional = "if" expr "then" expr "else" expr

lambda =
    "\" param-list "->" expr

func-application = expr "(" expr-list ")"

array-func = "[" ( array-sizes ":" )? index-mappings "]"

array-sizes = array-size { "," array-size }

array-size = expr | "~"

index-mappings = index-mapping ( ";" index-mapping )*  ";"?

index-mapping =
    index-pattern "->" expr
    |
    index-pattern ( "|" index-guard "->" expr )* "|" expr

index-pattern = index-pattern-elem ( "," index-pattern-elem )*

index-pattern-elem = id | int

index-guard = expr

array-enum = "[" array-enum-elem ( ";" array-enum-elem )*  "]"

array-enum-elem = expr

array-concat = expr "++" expr

array-application = array-app-object "[" expr-list "]"

array-app-object = expr | "this"

array-size = "#" array-size-object ( "@" array-size-dim )?

array-size-object = expr

array-size-dim = expr

let-expr =
    "let" binding "in" expr
    |
    "let" "{" bindings "}" "in" expr

where-expr =
    expr "where" binding
    |
    expr "where" "{" bindings "}"

expr-list = expr ( "," expr )*

id = [a-zA-Z] [a-zA-Z0-9_]*

qualified-id = id "." id

literal = int | real | complex | boolean

int = [0-9]+

real = [0-9]+ "." [0-9]+

complex = [0-9]+ ( "." [0-9]+ )? "i"

boolean = "true" | "false"
